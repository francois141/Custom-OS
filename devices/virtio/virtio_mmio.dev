/*
 * Copyright (c) 2014, ETH Zurich. All rights reserved.
 * Copyright (c) 2022, The University of British Columbia. All rights reserved.
 *
 * This file is distributed under the terms in the attached LICENSE file.
 * If you do not find this file, copies can be found by writing to:
 * ETH Zurich D-INFK, Universitaetsstrasse 6, CH-8092 Zurich. Attn: Systems Group.
 */

/*
 * virtio_mmio.dev
 *
 * Virtio over Memory Mapped IO
 *
 * From the Virtio Specification, Section 4.2.2
 *
 */

device virtio_mmio lsbfirst ( addr base ) "Virtio MMIO Transport Specification" {

    constants virtio_magic width(32) "Little Endian equivalent of the 'virt' string" {
        magic_value = 0x74726976  "Little Endian equivalent of the 'virt' string";
    };

    // The MagicValue Register
    register MagicValue ro addr(base, 0x000) "Magic value for identifying the Virtio device" type(virtio_magic);



    constants virtio_version width(32) "Virtio MMIO Device Versions" {
        virtio_version_invalid  = 0x0 "Invalid Version.";
        virtio_version_legacy   = 0x1 "The legacy interface is used.";
        virtio_version_virtio10 = 0x2 "Virtio Version 1.0";
    };

    register Version ro addr(base, 0x004) "Device Version Number" {
        version  8 type(virtio_version)  "Virtio device interface version";
        _       24                       "Reserved";
    };



    /*
     * Virtio Subsystem Device ID
     *
     * See 5 Device Types for possible values. Value zero (0x0) is used to de-
     * fine a system memory map with placeholder devices at static, well known
     * addresses, assigning functions to them depending on user's needs.
     */
    constants virtio_deviceid width(32) "Virtio Device IDs" {
        virtio_reserved        = 0x0 "Invalid Device ID";
        virtio_network_card    = 0x1 "Network Interface Device";
        virtio_block_device    = 0x2 "Block Device";
        virtio_console         = 0x3 "Serial Console Device";
        virtio_entropy_source  = 0x4 "Entorpy Source Device (Randomness)";
        virtio_legacy_balloon  = 0x5 "Memory Ballooning Device (legacy)";
        virtio_io_memory       = 0x6 "IO Memory Device";
        virtio_rpmsg           = 0x7 "RPMSG Device";
        virtio_scsi_host       = 0x8 "SCSI Host Device";
        virtio_transport_9p    = 0x9 "9P Transport Device";
        virtio_mac80211_wlan   = 0xA "MAC 802.11 WLAN Device";
        virtio_rproc_serial    = 0xB "RPROC Serial Device";
        virtio_virtio_caif     = 0xC "Virtio CAIF Device";
        virtio_memory_balloon  = 0xD "Memory Ballooning Device";
        virtio_gpu_device      = 0xE "GPU Device";
        virtio_timer_device    = 0xF "Clock / Timer Device";
    };

    register DeviceID ro addr(base, 0x008) "Virtio Subsystem Device ID" {
        id  8 type(virtio_deviceid)  "Device ID";
        _  24                        "Reserved";
    };


    // Virtio Subsystem Vendor ID
    register VendorID ro addr(base, 0x00C) "Virtio Subsystem Vendor ID" type(uint32);


    /*
     * Flags representing features the device supports
     *
     * Reading from this register returns 32 consecutive flag bits, first bit
     * depending on the last value written to DeviceFeaturesSel. Access to this
     * register returns bits DeviceFeaturesSel * 32 to
     * (DeviceFeaturesSel * 32) + 31, eg. feature bits 0 to 31 if
     *  DeviceFeaturesSel is set to 0 and features bits 32 to 63 if
     *  DeviceFeaturesSel is set to 1. Also see 2.2 Feature Bits.
     *
     *  Note: The representation of the actual feature bits depend on the device
     */
    register DeviceFeatures ro addr(base, 0x010) "Flags representing features the device supports" {
        features 32 "Virtio Features Bits";
    };



    /*
     * Device (host) features word selection.
     *
     * Writing to this register selects a set of 32 device feature bits accessible
     * by reading from DeviceFeatures.
     */
    register DeviceFeaturesSel addr(base, 0x014) "Device (host) features word selection." {
        ready     1 "The host has loaded the dev_features register";
        _        30 "Reserved";
        selector  1 "Virtio Feature Selector";
    };



    /*
     * Flags representing device features understood and activated by the driver
     *
     * Writing to this register sets 32 consecutive flag bits, first bit depending
     * on the last value written to DriverFeaturesSel. Access to this register sets
     * bits DriverFeaturesSel * 32 to (DriverFeaturesSel * 32) + 31, eg. feature
     * bits 0 to 31 if DriverFeaturesSel is set to 0 and features bits 32 to 63
     * if DriverFeaturesSel is set to 1. Also see 2.2 Feature Bits.
     *
     *  Note: The representation of the actual feature bits depend on the device
     */
    register DriverFeatures addr(base, 0x020) "Flags representing device features understood and activated by the driver" {
        features 32 "Virtio Features Bits";
    };


    register DriverFeaturesSel addr(base, 0x024) "Activated (guest) features word selection" {
        selector  1 "Virtio Feature Selector";
        _        29 "Reserved";
        ready     1 "signal the host that the values are ready";
        ack       1 "the host has stored the values";
    };



    /*
     * Virtual queue index
     *
     * Writing to this register selects the virtual queue that the following
     * operations on QueueNumMax, QueueNum, QueueReady, QueueDescLow,
     * QueueDescHigh, QueueAvailLow, QueueAvailHigh, QueueUsedLow and
     * QueueUsedHigh apply to. The index number of the first queue is zero (0x0).
     */
    register QueueSel addr(base, 0x030) "Virtual queue index" type(uint32);



    /*
     * Maximum virtual queue size
     *
     * Reading from the register returns the maximum size (number of elements)
     * of the queue the device is ready to process or zero (0x0) if the queue is
     * not available. This applies to the queue selected by writing to QueueSel.
     */
    register QueueNumMax ro addr(base, 0x34) "Maximum virtual queue size" type(uint32);



    /*
     * Virtual queue size
     *
     * Queue size is the number of elements in the queue, therefore size of the
     * Descriptor Table and both Available and Used rings. Writing to this
     * register notifies the device what size of the queue the driver will use.
     * This applies to the queue selected by writing to QueueSel.
     */
    register QueueNum addr(base, 0x038) "Virtual queue size" type(uint32);



    /*
     * Virtual queue ready bit
     *
     * Writing one (0x1) to this register notifies the device that the virtual
     * queue is ready to be used. Reading from this register returns the last
     * value written to it. Both read and write accesses apply to the queue
     * selected by writing to QueueSel.
     */
    register QueueReady addr(base, 0x044) "Virtual queue ready bit" {
        ready  1 "Queue ready bit";
        _     30 "Reserved";
        signal 1 "signal the host that something has changed";
    };

    constants queue width(1) "Queue Ready Bit Values" {
        ready    = 0x1 "The queue is ready to use";
        notready = 0x0 "The queue is not ready";
    };



    /*
     * Writing a queue index to this register notifies the device that there are
     * new buffers to process in the queue.
     */
    register QueueNotify addr(base, 0x050) "Queue notifier" type(uint32);


    /*
     * Reading from this register returns a bit mask of events that caused the
     * device interrupt to be asserted. The following events are possible:
     *
     * Used Ring Update - bit 0 - the interrupt was asserted because the device
     * has updated the Used Ring in at least one of the active virtual queues.
     *
     * Configuration Change - bit 1 - the interrupt was asserted because the
     * configuration of the device has changed.
     */
    register InterruptStatus ro addr(base, 0x060) "Interrupt status" {
        ring_update    1 "The device has updated the used ring";
        config_change  1 "The configuration of the device has changed";
        _             30 "";
    };



    /*
     * Writing to this register notifies the device that the interrupt has been
     * handled, as per values for InterruptStatus.
     *
     */
    register InterruptACK addr(base, 0x064) "Interrupt acknowledge" {
        ring_update    1 "The device has updated the used ring";
        config_change  1 "The configuration of the device has changed";
        _             30 "";
    };

    constants device_status width(8) "Reset value" {
        device_reset = 0x0 "Reset the device";
    };

    /*
     * Reading from this register returns the current device status flags.
     * Writing non-zero values to this register sets the status flags, indicating
     * the driver progress. Writing zero (0x0) to this register triggers a
     * device reset.
     */
    register Status addr(base, 0x70) {
        acknowledge 1   "Guest has found the device";
        driver      1   "Guest knows how to drive the device";
        driver_ok   1   "Driver setup and ready to drive the device";
        features_ok 1   "Driver has acknowledged all the features it understands";
        _           3   "Reserved";
        failed      1   "Something went wrong";
        _          24 "Reserved";
    };

    register Reset also addr(base, 0x70) type(uint32);



    /*
     * Virtual queue's Descriptor Table 64 bit long physical address
     *
     * Writing to these two registers (lower 32 bits of the address to QueueDescLow,
     * higher 32 bits to QueueDescHigh) notifies the device about location of
     * the Descriptor Table of the queue selected by writing to QueueSel register.
     */
    register QueueDescLow addr(base, 0x080) "Virtual queue Descriptor Table 64 bit long physical address" type(uint32);
    register QueueDescHigh addr(base, 0x084) "Virtual queue Descriptor Table 64 bit long physical address" type(uint32);



    /*
     * Virtual queue's Available Ring 64 bit long physical address
     *
     * Writing to these two registers (lower 32 bits of the address to
     * QueueAvailLow, higher 32 bits to QueueAvailHigh) notifies the device
     * about location of the Available Ring of the queue selected by writing to QueueSel.
     */
    register queue_avail_lo addr(base, 0x090) "Virtual queue Available Ring 64 bit long physical address" type(uint32);
    register queue_avail_hi addr(base, 0x094) "Virtual queue Available Ring 64 bit long physical address" type(uint32);



    /*
     * Virtual queue's Used Ring 64 bit long physical address
     *
     * Writing to these two registers (lower 32 bits of the address to
     * QueueUsedLow, higher 32 bits to QueueUsedHigh) notifies the device about
     * locationof the Used Ring of the queue selected by writing to QueueSel.
     */
    register queue_used_lo addr(base, 0x0a0) "Virtual queue Used Ring 64 bit long physical address" type(uint32);
    register queue_used_hi addr(base, 0x0a4) "Virtual queue Used Ring 64 bit long physical address" type(uint32);


    /*
     * Changes every time the configuration noticeably changes
     */
    register ConfigGeneration ro addr(base, 0x0fc) "Configuration atomicity value" type(uint32);
};
